class_name Gen2ModState
extends RefCounted

## The installation's own choices about installed mods: which are switched off,
## and which one's view the game is drawn with. Disabled ids are stored rather
## than enabled ones, so a mod just installed runs without needing an entry, and a
## file lost or damaged means every mod runs rather than none. A disabled mod is
## still discovered and still listed; only [method Gen2ModHost.load_discovered]
## skips it, so the launcher can switch it back on without reinstalling.

## Named for the list it was written for; it now also carries the selected view.
const PATH: String = "user://mods_disabled.json"

static var _disabled: Dictionary = {}
## The id whose renderers the game is drawn with, or the built-in one. Stored as
## a bare id and never validated here: whether the mod behind it is installed,
## enabled and registered is [Gen2ModHost]'s question, and it is asked every time
## a surface builds a renderer rather than once at load.
static var _view: StringName = Gen2ModHost.BUILT_IN_RENDERER
static var _loaded: bool = false


## The selected view's id. See [method Gen2ModHost.select_view].
static func selected_view() -> StringName:
	_ensure_loaded()
	return _view


## Returns false only when the change could not be written, in which case the
## in-memory value is rolled back so it never disagrees with the file.
static func set_selected_view(id: StringName) -> bool:
	_ensure_loaded()
	if String(id).is_empty():
		return false
	var previous: StringName = _view
	_view = id
	if _write():
		return true
	_view = previous
	return false


static func is_enabled(id: StringName) -> bool:
	_ensure_loaded()
	return not _disabled.has(id)


static func disabled_ids() -> Array[StringName]:
	_ensure_loaded()
	var out: Array[StringName] = []
	for id: StringName in _disabled:
		out.append(id)
	out.sort()
	return out


## As [method set_selected_view], for the enabled set.
static func set_enabled(id: StringName, enabled: bool) -> bool:
	_ensure_loaded()
	if String(id).is_empty():
		return false
	var was_disabled: bool = _disabled.has(id)
	if enabled:
		_disabled.erase(id)
	else:
		_disabled[id] = true
	if _write():
		return true
	if was_disabled:
		_disabled[id] = true
	else:
		_disabled.erase(id)
	return false


static func set_all_enabled(ids: Array, enabled: bool) -> bool:
	_ensure_loaded()
	var previous: Dictionary = _disabled.duplicate()
	for id: Variant in ids:
		if enabled:
			_disabled.erase(StringName(id))
		else:
			_disabled[StringName(id)] = true
	if _write():
		return true
	_disabled = previous
	return false


## Drops an id entirely, for a mod that no longer exists. A removed mod that
## stayed in the file would silently switch itself off if it were reinstalled.
static func forget(id: StringName) -> bool:
	_ensure_loaded()
	if not _disabled.has(id):
		return true
	_disabled.erase(id)
	return _write()


static func reload() -> void:
	_loaded = false
	_disabled = {}
	_view = Gen2ModHost.BUILT_IN_RENDERER
	_ensure_loaded()


static func _ensure_loaded() -> void:
	if _loaded:
		return
	_loaded = true
	_disabled = {}
	_view = Gen2ModHost.BUILT_IN_RENDERER
	if not FileAccess.file_exists(PATH):
		return
	var file: FileAccess = FileAccess.open(PATH, FileAccess.READ)
	if file == null:
		return
	var text: String = file.get_as_text()
	file.close()
	var json := JSON.new()
	if json.parse(text) != OK:
		return
	if json.data is not Dictionary:
		return
	var stored_view: Variant = (json.data as Dictionary).get("view", "")
	if stored_view is String and not (stored_view as String).is_empty():
		_view = StringName(stored_view as String)
	var raw: Variant = (json.data as Dictionary).get("disabled", [])
	if raw is not Array:
		return
	for id: Variant in raw as Array:
		var name := StringName(String(id))
		if not String(name).is_empty():
			_disabled[name] = true


static func _write() -> bool:
	var ids: Array[String] = []
	for id: StringName in _disabled:
		ids.append(String(id))
	ids.sort()
	var file: FileAccess = FileAccess.open(PATH, FileAccess.WRITE)
	if file == null:
		return false
	file.store_string(JSON.stringify({"disabled": ids, "view": String(_view)}, "\t"))
	file.close()
	return true
