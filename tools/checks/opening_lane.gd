extends RefCounted

var _r: RefCounted = null

## Validates the cache records required by the first playable lane. This is a
## read-only cache audit: it never opens a ROM and never treats a missing
## opening record as a presentation fallback.
##   Godot --headless --path . -s res://tools/validate.gd -- opening_lane

## Longer than the whole boot: Crystal's movie is 2,340 frames and Gold and
## Silver's 2,355, and the title screen's music lands just behind either.
const OPENING_FRAME_CAP: int = 6000

## The twelve BG-map columns the hardware screen never reaches, in pixels: what
## the title draws around itself and how far it repeats.
const BAND: int = (Gen2Layout.TITLE_TILEMAP_COLUMNS - Gen2TitlePage.COLUMNS) \
	* PokeTiles.TILE_WIDTH

const REQUIRED_SECTIONS: Dictionary = {
	"maps": RomCache.WORLD_MAPS,
	"tilesets": RomCache.WORLD_TILESETS,
	"scripts": RomCache.WORLD_SCRIPTS,
	"text": RomCache.WORLD_TEXT,
	"movements": RomCache.WORLD_MOVEMENTS,
	"sprites": RomCache.OVERWORLD_SPRITES,
	"audio": RomCache.WORLD_AUDIO,
}


func run(r: RefCounted) -> void:
	_r = r
	for game_id: StringName in _r.GAME_IDS:
		if not _validate(game_id):
			_r.fail("%s: the opening lane's cache records are incomplete." % game_id)


func _validate(game_id: StringName) -> bool:
	var data: GameData = GameData.open(game_id)
	if data == null:
		print("FAIL %s: cache is not usable" % game_id)
		return false
	var failures: Array[String] = []
	if data.id != game_id:
		failures.append("source_profile_mismatch:%s" % data.id)
	var counts: Dictionary = {}
	var audio_counts: Dictionary = data.world_service_counts()
	failures.append_array(_audit_sections(data, counts))
	failures.append_array(_audit_starting_maps(data))
	failures.append_array(_audit_opening_text(data, game_id))
	failures.append_array(_audit_audio_assets(data, audio_counts))
	if counts.get("movements", 0) == 0:
		failures.append("missing_movement_data")
	failures.append_array(_audit_audio_records(data, audio_counts))
	failures.append_array(_audit_opening_music(data))
	failures.append_array(_audit_title_backdrop(data))

	var script_audit: Dictionary = _audit_scripts(data, game_id == &"crystal")
	for reason: String in script_audit["failures"]:
		failures.append(reason)
	# Unknown commands are reported as coverage debt, not cache corruption: the
	# shared runner intentionally exposes source commands it does not yet model.
	# Empty payloads and malformed pointers remain hard failures above.
	if int(script_audit["malformed_pointers"]) > 0:
		failures.append("malformed_script_pointers:%d" % int(script_audit["malformed_pointers"]))
	print("%s: maps=%d scripts=%d text=%d movements=%d music=%d sfx=%d cries=%d" % [
		game_id, data.map_count(), int(counts.get("scripts", 0)), int(counts.get("text", 0)),
		int(counts.get("movements", 0)), int(audio_counts.get("music", 0)),
		int(audio_counts.get("sfx", 0)), int(audio_counts.get("cries", 0)),
	])
	print("  unknown_commands=%d malformed_pointers=%d" % [
		int(script_audit["unknown_commands"]), int(script_audit["malformed_pointers"]),
	])
	if failures.is_empty():
		print("  opening_lane=valid")
		return true
	print("  opening_lane=invalid failures=%s" % JSON.stringify(failures))
	return false


func _audit_sections(data: GameData, counts: Dictionary) -> Array[String]:
	var out: Array[String] = []
	for section: String in REQUIRED_SECTIONS:
		var path: String = "%s/%s" % [data.directory, REQUIRED_SECTIONS[section]]
		var value: Variant = RomCache.read_json(path)
		if (value is Dictionary and (value as Dictionary).is_empty()) \
			or (value is Array and (value as Array).is_empty()) \
			or value == null:
			out.append("missing_%s" % section)
		else:
			counts[section] = value.size()
	return out


func _audit_starting_maps(data: GameData) -> Array[String]:
	var out: Array[String] = []
	var home: Gen2WorldMap = data.world_map(24, Gen2WorldSpawn.PLAYERS_HOUSE_2F)
	var town: Gen2WorldMap = data.world_map(24, 4)
	if home == null:
		out.append("missing_bedroom_map:24:7")
	if town == null:
		out.append("missing_new_bark_map:24:4")
	for map: Gen2WorldMap in [home, town]:
		if map == null:
			continue
		var tileset: Gen2WorldTileset = data.world_tileset(map.tileset)
		if tileset == null or tileset.meta.is_empty() or tileset.collision.is_empty():
			out.append("missing_graphics_or_collision:map_%d_%d" % [map.group, map.number])
		if map.blocks.is_empty() or map.collision.is_empty():
			out.append("missing_map_payload:map_%d_%d" % [map.group, map.number])
	return out


func _audit_opening_text(data: GameData, game_id: StringName) -> Array[String]:
	var out: Array[String] = []
	for table: int in 4:
		if data.name_input_chars(table).is_empty():
			out.append("missing_name_keyboard:%d" % table)
	for key: String in ["oak_1", "oak_2", "oak_4", "oak_5", "oak_6", "oak_7"]:
		if data.intro_text(key).is_empty():
			out.append("missing_intro_text:%s" % key)
	if game_id == &"crystal" and data.intro_text("gender").is_empty():
		out.append("missing_crystal_gender_text")
	return out


func _audit_audio_assets(data: GameData, audio_counts: Dictionary) -> Array[String]:
	var out: Array[String] = []
	for kind: String in ["music", "sfx", "cries"]:
		if int(audio_counts.get(kind, 0)) <= 0:
			out.append("missing_audio:%s" % kind)
	for asset: StringName in [&"wave_samples", &"drumkits"]:
		if data.world_audio_asset_bytes(asset).is_empty():
			out.append("missing_audio_payload:%s" % asset)
	return out


func _audit_audio_records(data: GameData, audio_counts: Dictionary) -> Array[String]:
	var out: Array[String] = []
	for kind: StringName in [&"music", &"sfx", &"cries"]:
		for index: int in int(audio_counts.get(String(kind), 0)):
			var record: Dictionary = data.world_audio(kind, index)
			if record.is_empty():
				out.append("missing_audio_record:%s:%d" % [kind, index])
			elif record.get("bytes", PackedByteArray()).is_empty():
				out.append("missing_audio_record_payload:%s:%d" % [kind, index])
	return out

## What the title screen puts around itself in a window that is not 10:9.
## `LoadTitleScreenTilemap` writes all thirty-two columns of the BG map and the
## hardware shows twenty, so the surround is the twelve the screen never reached,
## repeated. Two things would break it and neither is visible in a 160x144
## capture: the band being anything but those twelve columns, which brings the
## logo round a second time, and the vertical edges wrapping rather than
## clamping, which puts the cloud bank above the sky.
func _audit_title_backdrop(data: GameData) -> Array[String]:
	var out: Array[String] = []
	var page: Gen2TitlePage = Gen2TitlePage.from_data(data)
	if page == null:
		return ["missing_title_art"]
	## A window wider and taller than the hardware's, on the block grid the
	## screen rounds a buffer up to.
	var view := Vector2i(Gen2Screen.WIDTH + 4 * BAND, Gen2Screen.HEIGHT + 64)
	var origin := Vector2i(2 * BAND, 32)
	var backdrop: Image = page.draw_backdrop(view, origin)
	if backdrop.get_size() != view:
		return ["title_backdrop_size:%s" % backdrop.get_size()]
	## Every row, not a sampled one: the sky rows are that one field tile whatever
	## the band is, and only the cloud bank's four-tile pattern says how wide it
	## really is.
	for x: int in BAND:
		for y: int in view.y:
			if backdrop.get_pixel(x, y) != backdrop.get_pixel(x + BAND, y):
				out.append("title_surround_not_the_unseen_band:%d,%d" % [x, y])
				break
	for y: int in view.y:
		var inside: int = clampi(y, origin.y, origin.y + Gen2Screen.HEIGHT - 1)
		if backdrop.get_pixel(0, y) != backdrop.get_pixel(0, inside):
			out.append("title_surround_row_not_clamped:%d" % y)
	return out


func _audit_scripts(data: GameData, crystal: bool) -> Dictionary:
	var raw: Variant = RomCache.read_json(RomCache.world_scripts_path(data.directory))
	var failures: Array[String] = []
	var unknown_commands: int = 0
	var malformed_pointers: int = 0
	if not raw is Dictionary:
		return {"failures": ["missing_script_index"], "unknown_commands": 0, "malformed_pointers": 0}
	for key: String in (raw as Dictionary):
		var parts: PackedStringArray = key.split(":")
		if parts.size() != 2:
			malformed_pointers += 1
			continue
		var bank: int = int(parts[0])
		var address: int = ("0x%s" % parts[1]).hex_to_int()
		var bytes: PackedByteArray = data.world_script(bank, address)
		if bytes.is_empty():
			malformed_pointers += 1
			continue
		var at: int = 0
		for _step: int in Gen2WorldScript.MAX_COMMANDS:
			var command: Dictionary = Gen2WorldScript.command_at(bytes, at, crystal)
			if not bool(command.get("ok", false)):
				unknown_commands += 1
				break
			at += int(command.get("width", 1))
			if not Gen2WorldScript.continues_after(int(command.get("opcode", 0)), crystal):
				break
	return {
		"failures": failures,
		"unknown_commands": unknown_commands,
		"malformed_pointers": malformed_pointers,
	}


## The opening's own `PlayMusic` calls, driven through the live screen rather
## than counted off the coordinator: every one of them has to reach the driver,
## which is the half `tests/unit/test_boot_cinema.gd` cannot see. The whole boot
## is run to the title screen's own `PlayMusic MUSIC_TITLE`, so the movie's
## request is spent on the way.
func _audit_opening_music(data: GameData) -> Array[String]:
	var tree: SceneTree = Engine.get_main_loop() as SceneTree
	if tree == null:
		return ["no_scene_tree_for_opening_music"]
	var splash := Gen2SplashScreen.new()
	tree.root.add_child(splash)
	# The screen counts its own frames off the clock in `_process`; this check
	# spends them by hand, the way `tools/preview_*.gd` do.
	splash.set_process(false)
	var out: Array[String] = []
	if not splash.open(data):
		out.append("splash_did_not_open")
		tree.root.remove_child(splash)
		splash.free()
		return out
	var movie_music: int = -1
	var movie_frame: int = 0
	var title_music: int = -1
	var frames: int = 0
	var seen: int = -1
	while frames < OPENING_FRAME_CAP and title_music < 0:
		splash.advance_frames(1)
		frames += 1
		var request: Dictionary = splash.last_music_request()
		if int(request.get("frame", -1)) == seen or int(request.get("music", 0)) <= 0:
			continue
		seen = int(request.get("frame", -1))
		if not bool(request.get("played", false)):
			out.append("opening_music_refused:%d" % int(request.get("music", 0)))
			break
		if splash.visible_image() == &"title":
			title_music = int(request["music"])
		elif movie_music < 0:
			movie_music = int(request["music"])
			movie_frame = seen
	if movie_music < 0:
		out.append("the movie asked the driver for no music")
	if title_music != Gen2BootCinema.MUSIC_TITLE:
		out.append("the title screen asked the driver for music %d" % title_music)
	elif not splash.music_playing():
		out.append("the title screen's music did not reach the driver")
	print("  opening_music: movie=%d at frame %d, title=%d" % [
		movie_music, movie_frame, title_music,
	])
	tree.root.remove_child(splash)
	splash.free()
	return out
