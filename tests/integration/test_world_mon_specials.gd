extends GutTest

## Scene integration for the routines that open `SelectMonFromParty`:
## `NameRater` and `MoveDeletion`. Each is its own line through the overworld,
## two `YesNoBox`es, the party list, and an ending text.
##
## Both maps are `opentext / special / waitbutton / closetext / end`, so the last
## text either special prints is dismissed by the script's own `waitbutton` and
## not by the special. That ordering is what this covers; [Gen2NameRater]'s and
## [Gen2MoveDeleter]'s own branches are unit tested beside the party host.

const Fixture := preload("res://tests/integration/world_trainer_fixture.gd")

const PLAYER_CELL: Vector2i = Vector2i(2, 2)
const TALK_CELL: Vector2i = Vector2i(4, 5)

var _data: GameData = null
var _world_screen: Gen2WorldScreen = null


func before_each() -> void:
	Fixture.build()
	_write_name_rater_script()
	_data = GameData.open_directory(Fixture.directory())
	Gen2OptionsStore.use_test_path()
	DirAccess.remove_absolute(Gen2OptionsStore.path())


func after_each() -> void:
	## The routine drops its party list and its naming screen with `queue_free`,
	## which the tree runs on the next frame and this test never spends: the
	## presses above are hardware frames, not process ones.
	await get_tree().process_frame
	if is_instance_valid(_world_screen):
		_world_screen.free()
		_world_screen = null
	RomCache.clear(Fixture.directory())


func _write_name_rater_script(
	special: int = Gen2WorldScriptRunner.SPECIAL_NAME_RATER
) -> void:
	var directory: String = Fixture.directory()
	var scripts: Dictionary = RomCache.read_json(RomCache.world_scripts_path(directory))
	scripts[Gen2WorldScript.pointer_key(Fixture.BANK, Fixture.TUTORIAL_SCRIPT)] = [
		Gen2WorldScript.SPECIAL, special, 0x00,
		Gen2WorldScript.WAITBUTTON,
		Gen2WorldScript.END,
	]
	RomCache.write_json(RomCache.world_scripts_path(directory), scripts)


func _open_world() -> void:
	var packed: PackedScene = load("res://game/world/world_screen.tscn")
	_world_screen = packed.instantiate() as Gen2WorldScreen
	_world_screen.map_group = Fixture.MAP_GROUP
	_world_screen.map_number = Fixture.MAP_NUMBER
	_world_screen.start_cell = PLAYER_CELL
	var world: Gen2WorldAPI = Gen2WorldAPI.open(
		_data, Fixture.MAP_GROUP, Fixture.MAP_NUMBER, PLAYER_CELL, Gen2WorldState.new()
	)
	var save: Gen2SaveData = Gen2SaveStore.create_development_save(_data, 0)
	save.world = world.snapshot()
	world.set_player_id(save.player_id)
	_world_screen.set_data(_data)
	_world_screen.set_save(save)
	add_child(_world_screen)
	await get_tree().process_frame
	## The box reveals on hardware frames and this test counts presses, so the
	## frames it spends have to be the ones it asks for.
	_world_screen.set_process(false)
	var mon: Gen2SaveMon = save.party[0]
	mon.is_egg = false
	mon.nickname = "SPARKY"
	mon.original_trainer = save.player_name
	mon.ot_id = save.player_id


func _run_script() -> void:
	_world_screen._show_script_results(
		_world_screen._world.dispatch_script_events(TALK_CELL)
	)


func _host() -> Gen2NameRaterScreen:
	return _world_screen._name_rater_host


## Spends whatever the routine's box still owes, which is what a player waits
## through, plus the frame `advance_frame` reads it on: `PrintText` returning is
## what opens a `YesNoBox`, and no press can shorten the printing.
func _settle() -> void:
	var guard: int = 600
	while guard > 0:
		var host: Gen2NameRaterScreen = _host()
		if host == null or host._text_box == null or not host._text_box.is_revealing():
			break
		_world_screen.advance_frame()
		guard -= 1
	_world_screen.advance_frame()


func _press(button: int) -> void:
	_settle()
	_world_screen.press_button(button)


## Walks to the party list: hello, YES, which_mon, the press `prompt` waits for.
func _reach_party() -> void:
	_run_script()
	_press(PokeButton.A)
	assert_eq(_host().phase(), Gen2NameRaterScreen.Phase.WHICH_MON)
	_press(PokeButton.A)


func test_the_special_opens_the_routine_and_holds_the_world() -> void:
	await _open_world()
	_run_script()
	assert_not_null(_host())
	assert_false(_world_screen.move_player(Vector2i.RIGHT))
	assert_false(_world_screen.interact())
	_settle()
	assert_eq(_host().phase(), Gen2NameRaterScreen.Phase.HELLO_ASK)
	assert_eq(_host().question_cursor(), 0)


## `jp c, .cancel`: NO on the first question is `NameRaterComeAgainText`, and
## nothing after it runs.
func test_no_on_the_introduction_ends_on_come_again() -> void:
	await _open_world()
	_run_script()
	_settle()
	_world_screen.press_button(PokeButton.DOWN)
	_world_screen.press_button(PokeButton.A)
	assert_null(_host())
	assert_true(_world_screen._text_box.visible)
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.name_rater_text("come_again").split("\n"))
	)


## The ending text is the script's, not the special's: `special NameRater`
## returns with it standing and the map's own `waitbutton` is the press.
func test_the_ending_text_waits_on_the_scripts_own_waitbutton() -> void:
	await _open_world()
	_run_script()
	_settle()
	_world_screen.press_button(PokeButton.B)
	assert_eq(
		StringName(_world_screen._world.pending_script_input().get("type", &"")), &"button"
	)


func test_the_party_list_is_select_mon_from_party() -> void:
	await _open_world()
	_reach_party()
	var party: Gen2PartyScreen = _host().party_screen()
	assert_not_null(party)
	assert_eq(party._prompt(), Gen2PartyScreen.PROMPT_CHOOSE)
	## A on a member answers the caller rather than opening `MonSubmenu`.
	party.handle_button(PokeButton.A)
	assert_null(_host().party_screen())
	assert_eq(_host().phase(), Gen2NameRaterScreen.Phase.BETTER_NAME)


## `PartyMenuSelect`'s carry: B over the list is the same `.cancel` a NO is.
func test_cancelling_the_party_list_ends_on_come_again() -> void:
	await _open_world()
	_reach_party()
	_host().party_screen().handle_button(PokeButton.B)
	assert_null(_host())
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.name_rater_text("come_again").split("\n"))
	)


## `.traded`: `CheckIfMonIsYourOT` is read before either question about the
## name, so a traded member never reaches the naming screen.
func test_a_traded_member_ends_on_perfect_name_with_its_nickname_filled() -> void:
	await _open_world()
	_world_screen.active_save().party[0].ot_id += 1
	_reach_party()
	_host().party_screen().handle_button(PokeButton.A)
	assert_null(_host())
	var shown: String = " ".join(_world_screen._text_box.text_lines())
	assert_true(shown.contains("SPARKY"), shown)
	assert_false(shown.contains(Gen2TextStream.RAM_MARKER), shown)


## `.egg`: the EGG test runs before `GetCurNickname`, so an egg is refused
## whoever caught it.
func test_an_egg_ends_on_the_egg_text() -> void:
	await _open_world()
	_world_screen.active_save().party[0].is_egg = true
	_reach_party()
	_host().party_screen().handle_button(PokeButton.A)
	assert_null(_host())
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.name_rater_text("egg").split("\n"))
	)


## The whole line through, ending in the `CopyBytes` into the party row.
func test_a_new_name_is_written_to_the_party_row() -> void:
	await _open_world()
	_reach_party()
	_host().party_screen().handle_button(PokeButton.A)
	_settle()
	assert_eq(_host().phase(), Gen2NameRaterScreen.Phase.BETTER_ASK)
	_world_screen.press_button(PokeButton.A)
	assert_eq(_host().phase(), Gen2NameRaterScreen.Phase.WHAT_NAME)
	_press(PokeButton.A)
	assert_eq(_host().phase(), Gen2NameRaterScreen.Phase.NAMING)

	var naming: Gen2NamingScreenScreen = _host().naming_screen()
	assert_not_null(naming)
	naming.closed.emit("BOLT")
	assert_eq(_host().phase(), Gen2NameRaterScreen.Phase.NAMED)
	_press(PokeButton.A)
	assert_null(_host())
	assert_eq(_world_screen.active_save().party[0].nickname, "BOLT")
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.name_rater_text("finished").split("\n"))
	)


## `IsNewNameEmpty` reaching `.samename`: `NameRaterSameNameText` prints and the
## row keeps the name it had.
func test_an_empty_entry_leaves_the_row_alone() -> void:
	await _open_world()
	_reach_party()
	_host().party_screen().handle_button(PokeButton.A)
	_settle()
	_world_screen.press_button(PokeButton.A)
	_press(PokeButton.A)
	_host().naming_screen().closed.emit("")
	_press(PokeButton.A)
	assert_null(_host())
	assert_eq(_world_screen.active_save().party[0].nickname, "SPARKY")
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.name_rater_text("same_name").split("\n"))
	)


## The move deleter from here down. Same harness, one special further on: the
## script is rewritten before the world is opened, so `_host()` below is the
## other routine's.
func _open_deleter_world() -> void:
	_write_name_rater_script(Gen2WorldScriptRunner.SPECIAL_MOVE_DELETION)
	_data = GameData.open_directory(Fixture.directory())
	await _open_world()
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	mon.moves = [1, 2, 0, 0]
	mon.pp = [10, 20, 0, 0]


func _deleter() -> Gen2MoveDeleterScreen:
	return _world_screen._move_deleter_host


func _settle_deleter() -> void:
	var guard: int = 600
	while guard > 0:
		var host: Gen2MoveDeleterScreen = _deleter()
		if host == null or host._text_box == null or not host._text_box.is_revealing():
			break
		_world_screen.advance_frame()
		guard -= 1
	_world_screen.advance_frame()


## Walks to the move list: intro, YES, which_mon, the party list, the member,
## which_move, the press `prompt` waits for.
func _reach_move_list() -> void:
	_run_script()
	_settle_deleter()
	_world_screen.press_button(PokeButton.A)
	_settle_deleter()
	_world_screen.press_button(PokeButton.A)
	_deleter().party_screen().handle_button(PokeButton.A)
	_settle_deleter()
	_world_screen.press_button(PokeButton.A)


func test_the_deleter_special_opens_its_own_routine() -> void:
	await _open_deleter_world()
	_run_script()
	assert_not_null(_deleter())
	assert_null(_world_screen._name_rater_host)
	_settle_deleter()
	assert_eq(_deleter().phase(), Gen2MoveDeleterScreen.Phase.INTRO_ASK)


## `.onlyonemove` is read off the second slot, before the move list is ever
## drawn.
func test_a_member_with_one_move_is_refused_before_the_list() -> void:
	await _open_deleter_world()
	_world_screen.active_save().party[0].moves = [1, 0, 0, 0]
	_run_script()
	_settle_deleter()
	_world_screen.press_button(PokeButton.A)
	_settle_deleter()
	_world_screen.press_button(PokeButton.A)
	_deleter().party_screen().handle_button(PokeButton.A)
	assert_null(_deleter())
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.move_deleter_text("knows_one").split("\n"))
	)


## `DeleteMoveScreen2DMenuData` accepts up, down, A and B and nothing else, so
## the list neither cycles between members nor holds a move.
func test_the_move_list_neither_cycles_nor_swaps() -> void:
	await _open_deleter_world()
	_reach_move_list()
	var moves: Gen2MoveScreen = _deleter().move_screen()
	assert_not_null(moves)
	assert_false(moves.handle_button(PokeButton.RIGHT))
	assert_false(moves.handle_button(PokeButton.LEFT))
	assert_eq(moves.snapshot()["held"], -1)


## The whole line through: the move and its PP leave together and the ending
## text is the script's.
func test_a_deleted_move_takes_its_pp_with_it() -> void:
	await _open_deleter_world()
	_reach_move_list()
	_deleter().move_screen().handle_button(PokeButton.DOWN)
	_deleter().move_screen().handle_button(PokeButton.A)
	assert_eq(_deleter().phase(), Gen2MoveDeleterScreen.Phase.DELETE_ASK)
	_settle_deleter()
	_world_screen.press_button(PokeButton.A)
	assert_null(_deleter())
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	assert_eq(mon.moves, [1, 0, 0, 0])
	assert_eq(mon.pp, [10, 0, 0, 0])
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.move_deleter_text("forgot").split("\n"))
	)


## NO on the second question is `.declined`, and nothing is written.
func test_no_on_the_confirmation_leaves_the_moves_alone() -> void:
	await _open_deleter_world()
	_reach_move_list()
	_deleter().move_screen().handle_button(PokeButton.A)
	_settle_deleter()
	_world_screen.press_button(PokeButton.B)
	assert_null(_deleter())
	assert_eq(_world_screen.active_save().party[0].moves, [1, 2, 0, 0])
	assert_eq(
		" ".join(_world_screen._text_box.text_lines()),
		" ".join(_data.move_deleter_text("come_again").split("\n"))
	)


## `MoveTutor` from here down, one special further on again. Its map script is
## `setval` then `special`, since `.GetMoveTutorMove` reads the value the
## `verticalmenu` in front of it left.
const TUTOR_MOVE: int = 0x35


func _write_tutor_script(value: int) -> void:
	var directory: String = Fixture.directory()
	var scripts: Dictionary = RomCache.read_json(RomCache.world_scripts_path(directory))
	scripts[Gen2WorldScript.pointer_key(Fixture.BANK, Fixture.TUTORIAL_SCRIPT)] = [
		Gen2WorldScript.SETVAL, value,
		Gen2WorldScript.SPECIAL, Gen2WorldScriptRunner.SPECIAL_MOVE_TUTOR, 0x00,
		Gen2WorldScript.WAITBUTTON,
		Gen2WorldScript.END,
	]
	RomCache.write_json(RomCache.world_scripts_path(directory), scripts)


## The three `add_mt` rows past HM07 and one species that learns the first of
## them, which is what `CanLearnTMHMMove` reads.
func _write_tutor_tables(learnable: bool) -> void:
	var directory: String = Fixture.directory()
	var table: Array = []
	for index: int in Gen2Layout.TMHM_TM_COUNT + Gen2Layout.TMHM_HM_COUNT:
		table.append(0x60 + index)
	table.append_array([TUTOR_MOVE, TUTOR_MOVE + 1, TUTOR_MOVE + 2])
	RomCache.write_json(RomCache.tmhm_moves_path(directory), table)
	var species: Array = RomCache.read_json(RomCache.species_path(directory))
	for raw: Dictionary in species:
		var flags: Array = []
		flags.resize(Gen2Layout.TMHM_BYTES)
		for index: int in flags.size():
			flags[index] = 0
		# TMNUM 58 is MT01, bit index 57 counted from the low bit of byte 7.
		flags[7] = 0x02 if learnable else 0x00
		raw["tmhm"] = flags
	RomCache.write_json(RomCache.species_path(directory), species)


func _open_tutor_world(
	value: int = Gen2MoveTutor.VALUE_FLAMETHROWER, learnable: bool = true
) -> void:
	Fixture.build()
	_write_tutor_script(value)
	_write_tutor_tables(learnable)
	_data = GameData.open_directory(Fixture.directory())
	await _open_world()
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	mon.moves = [1, 0, 0, 0]
	mon.pp = [10, 0, 0, 0]
	mon.happiness = 70


func _tutor() -> Gen2MoveTutorScreen:
	return _world_screen._move_tutor_host


## `MoveTutor` opens on `ChooseMonToLearnTMHM` with no box of its own: every
## line before the list belongs to the map script.
func test_the_tutor_special_opens_the_party_list_first() -> void:
	await _open_tutor_world()
	_run_script()
	assert_not_null(_tutor())
	assert_eq(_tutor().phase(), Gen2MoveTutorScreen.Phase.SELECT_MON)
	assert_not_null(_tutor().party_screen())
	assert_false(_world_screen.move_player(Vector2i.RIGHT))


## `.quit`'s `xor a`: a learned move answers FALSE, which is the branch the map
## script takes to `takecoins`.
func test_a_learned_move_answers_false_and_costs_happiness() -> void:
	await _open_tutor_world()
	_run_script()
	_tutor().party_screen().handle_button(PokeButton.A)
	assert_null(_tutor())
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	assert_eq(mon.moves, [1, TUTOR_MOVE, 0, 0])
	assert_eq(mon.happiness, 71)
	assert_eq(_world_screen._world._active_script._script_value, Gen2MoveTutor.SCRIPT_VALUE_LEARNED)


## `.cancel`'s `ld a, -1`: B on the list is the one exit that is not a learned
## move, and it writes nothing.
func test_backing_out_of_the_list_answers_minus_one() -> void:
	await _open_tutor_world()
	_run_script()
	_tutor().party_screen().handle_button(PokeButton.B)
	assert_null(_tutor())
	assert_eq(_world_screen.active_save().party[0].moves, [1, 0, 0, 0])
	assert_eq(_world_screen._world._active_script._script_value, Gen2MoveTutor.SCRIPT_VALUE_CANCELLED)


## `.didnt_learn` is `and a / ret`, and `jr nc, .loop` reads that as another
## pass: an incompatible member prints its line and comes back to the list
## rather than ending the special.
func test_an_incompatible_member_loops_back_to_the_list() -> void:
	await _open_tutor_world(Gen2MoveTutor.VALUE_FLAMETHROWER, false)
	_run_script()
	_tutor().party_screen().handle_button(PokeButton.A)
	assert_eq(_tutor().phase(), Gen2MoveTutorScreen.Phase.REFUSAL)
	assert_true(_tutor().box_text().contains("not compatible"))
	## The line is three lines of text and the box pages, so the presses that
	## dismiss it are the player's own; what matters is where they land.
	var guard: int = 8
	while guard > 0 and _tutor() != null \
		and _tutor().phase() == Gen2MoveTutorScreen.Phase.REFUSAL:
		_settle_tutor()
		_world_screen.press_button(PokeButton.A)
		guard -= 1
	assert_not_null(_tutor())
	assert_eq(_tutor().phase(), Gen2MoveTutorScreen.Phase.SELECT_MON)
	assert_eq(_world_screen.active_save().party[0].moves, [1, 0, 0, 0])


## `LearnMove` reaches `ForgetMove` last of the three, so a full moveset asks
## before anything is written, and `.hmmove` is `jr .loop` rather than a cancel.
func test_a_full_moveset_asks_and_refuses_an_hm_without_closing_the_list() -> void:
	await _open_tutor_world()
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	mon.moves = [Gen2MoveForget.HM_MOVES[0], 2, 3, 4]
	mon.pp = [10, 10, 10, 10]
	_run_script()
	_tutor().party_screen().handle_button(PokeButton.A)
	assert_eq(_tutor().phase(), Gen2MoveTutorScreen.Phase.FORGET_ASK)
	_settle_tutor()
	_world_screen.press_button(PokeButton.A)
	assert_eq(_tutor().phase(), Gen2MoveTutorScreen.Phase.FORGET_LIST)
	_world_screen.press_button(PokeButton.A)
	assert_eq(_tutor().phase(), Gen2MoveTutorScreen.Phase.FORGET_LIST, "the list stays open")
	assert_eq(_tutor().box_text(), Gen2MoveForget.cant_forget_hm_text())
	_settle_tutor()
	_world_screen.press_button(PokeButton.DOWN)
	_world_screen.press_button(PokeButton.A)
	assert_null(_tutor())
	assert_eq(_world_screen.active_save().party[0].moves[1], TUTOR_MOVE)
	assert_eq(_world_screen._world._active_script._script_value, Gen2MoveTutor.SCRIPT_VALUE_LEARNED)


func _settle_tutor() -> void:
	var guard: int = 600
	while guard > 0:
		var host: Gen2MoveTutorScreen = _tutor()
		if host == null or host._text_box == null or not host._text_box.is_revealing():
			break
		_world_screen.advance_frame()
		guard -= 1
	_world_screen.advance_frame()


## `engine/events/haircut.asm`'s four routines are the same `SelectMonFromParty`
## with no boxes of their own: every line the player reads belongs to the map
## script, so the special owes a party list and an answer and nothing else.
func _run_haircut(special: int) -> void:
	_write_name_rater_script(special)
	await _open_world()
	_run_script()


func _selection_list() -> Gen2PartyScreen:
	return _world_screen._party_host


func test_a_grooming_special_opens_the_party_list_with_no_box_of_its_own() -> void:
	await _run_haircut(Gen2WorldScriptRunner.SPECIAL_OLDER_HAIRCUT_BROTHER)
	assert_not_null(_selection_list())
	assert_eq(_selection_list()._prompt(), Gen2PartyScreen.PROMPT_CHOOSE)
	assert_false(_world_screen._text_box.visible, "the script owns every line")
	assert_false(_world_screen.move_player(Vector2i.RIGHT))


## `.nope`: the carry a B press or the CANCEL row answers with is `xor a`, which
## is the `ifequal $0` both haircut scripts refuse on.
func test_cancelling_the_list_answers_zero_and_changes_no_happiness() -> void:
	await _run_haircut(Gen2WorldScriptRunner.SPECIAL_YOUNGER_HAIRCUT_BROTHER)
	var before: int = _world_screen.active_save().party[0].happiness
	_selection_list().handle_button(PokeButton.B)
	assert_null(_selection_list())
	assert_eq(_world_screen._world._active_script._script_value, 0)
	assert_eq(_world_screen.active_save().party[0].happiness, before)


## `.egg`: `cp EGG` is read before the name copy and before `Random`, so an egg
## answers 1 and no row is walked.
func test_an_egg_answers_one_and_is_not_groomed() -> void:
	await _run_haircut(Gen2WorldScriptRunner.SPECIAL_DAISYS_GROOMING)
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	mon.is_egg = true
	var before: int = mon.happiness
	_selection_list().handle_button(PokeButton.A)
	assert_eq(_world_screen._world._active_script._script_value, 1)
	assert_eq(mon.happiness, before)


## `call ChangeHappiness` on the row `Random` picked, and `wCurPartySpecies`
## left holding the chosen member, which is what a following
## `special PlayCurMonCry` reads rather than the row in wScriptVar.
func test_grooming_raises_happiness_and_leaves_the_chosen_species_standing() -> void:
	await _run_haircut(Gen2WorldScriptRunner.SPECIAL_DAISYS_GROOMING)
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	mon.happiness = 100
	## One roll in 256 walks off the end of the table and changes nothing, which
	## is `HAPPINESS_TABLE_OVERRUN_OPCODE`'s own case and is covered where the
	## walk lives. Pin the roll so this case is the row and not the overrun.
	var script: Gen2WorldScriptRunner = _world_screen._world._active_script
	script._random.seed = 1
	_selection_list().handle_button(PokeButton.A)
	var runner: Gen2WorldScriptRunner = _world_screen._world._active_script
	assert_gt(mon.happiness, 100, "HAPPINESS_GROOMING is a rise at every threshold")
	assert_eq(runner._cur_party_species, mon.species)
	assert_ne(runner._script_value, mon.species,
		"wScriptVar carries the table row, which is why the cry reads the other byte")


## `BillsGrandfather` has no egg branch and no table: it answers the species
## itself, which is what `ifnotequal LICKITUNG` and its four siblings read.
func test_bills_grandfather_answers_the_chosen_species() -> void:
	await _run_haircut(Gen2WorldScriptRunner.SPECIAL_BILLS_GRANDFATHER)
	var mon: Gen2SaveMon = _world_screen.active_save().party[0]
	var before: int = mon.happiness
	_selection_list().handle_button(PokeButton.A)
	assert_eq(_world_screen._world._active_script._script_value, mon.species)
	assert_eq(mon.happiness, before, "no row is walked here")


## The three balance windows write the tilemap and return, so the script runs
## straight on and the box stands over the map until `closetext` redraws it.
func test_a_balance_window_stands_over_the_map_until_closetext() -> void:
	var directory: String = Fixture.directory()
	var scripts: Dictionary = RomCache.read_json(RomCache.world_scripts_path(directory))
	scripts[Gen2WorldScript.pointer_key(Fixture.BANK, Fixture.TUTORIAL_SCRIPT)] = [
		Gen2WorldScript.SPECIAL,
		Gen2WorldScriptRunner.SPECIAL_PLACE_MONEY_TOP_RIGHT, 0x00,
		Gen2WorldScript.WAITBUTTON,
		Gen2WorldScript.CLOSETEXT,
		Gen2WorldScript.END,
	]
	RomCache.write_json(RomCache.world_scripts_path(directory), scripts)
	await _open_world()
	_run_script()
	assert_true(_world_screen.money_window_open())
	_world_screen.press_button(PokeButton.A)
	assert_false(_world_screen.money_window_open())


## `Route35GoldenrodGate`'s `givepokemail GiftSpearowMail` and Route 31's
## `checkpokemail ReceivedSpearowMailText`, which are one site each in either
## corpus and the whole of Randy's Spearow errand. Both point at bytes that sit
## behind the script naming them rather than at a pointer of their own, so the
## fixture lays them out the same way.
const MAIL_ITEM: int = 158
const MAIL_LINE_1: String = "DARK CAVE leads"
const MAIL_LINE_2: String = "to another road"


func _mail_message(first: String, second: String) -> PackedByteArray:
	var out := PackedByteArray()
	out.append_array(Gen2Text.encode(first))
	out.append(Gen2Text.NEXT_LINE)
	out.append_array(Gen2Text.encode(second))
	out.append(Gen2Text.TERMINATOR)
	out.resize(Gen2SaveMail.MESSAGE_LENGTH)
	return out


## `[opcode, pointer, waitbutton, end]` and the operand bytes behind them, at an
## address inside this script's own cached slice.
func _write_mail_script(opcode: int, payload: PackedByteArray) -> void:
	var address: int = Fixture.TUTORIAL_SCRIPT + 5
	var bytes: Array = [
		opcode, address & 0xFF, (address >> 8) & 0xFF,
		Gen2WorldScript.WAITBUTTON,
		Gen2WorldScript.END,
	]
	bytes.append_array(Array(payload))
	var directory: String = Fixture.directory()
	var scripts: Dictionary = RomCache.read_json(RomCache.world_scripts_path(directory))
	scripts[Gen2WorldScript.pointer_key(Fixture.BANK, Fixture.TUTORIAL_SCRIPT)] = bytes
	RomCache.write_json(RomCache.world_scripts_path(directory), scripts)


## Trims the development save's party down to its first member, so
## `CheckCurPartyMonFainted` has nobody left to walk home with.
func _leave_one_member() -> void:
	var save: Gen2SaveData = _world_screen.active_save()
	save.party.resize(1)
	_world_screen._refresh_party_summary()


func _carry_mail(mon: Gen2SaveMon, message: PackedByteArray) -> void:
	mon.item = MAIL_ITEM
	mon.mail = Gen2SaveMail.from_script(
		message, mon.original_trainer, mon.ot_id, mon.species, MAIL_ITEM
	)
	_world_screen._refresh_party_summary()


func _run_mail_script(opcode: int, payload: PackedByteArray) -> void:
	_write_mail_script(opcode, payload)
	await _open_world()
	_run_script()


## `GivePokeMail` hangs the item on the last party member and copies the
## script's own bytes into its mail row. The author is the member's OT rather
## than the player's name, which is what `wPartyMonOTs` is.
func test_givepokemail_writes_the_item_and_the_message_onto_the_last_member() -> void:
	var message: PackedByteArray = _mail_message(MAIL_LINE_1, MAIL_LINE_2)
	var payload := PackedByteArray([MAIL_ITEM])
	payload.append_array(message)
	await _run_mail_script(Gen2WorldScript.GIVEPOKEMAIL, payload)
	var save: Gen2SaveData = _world_screen.active_save()
	var last: Gen2SaveMon = save.party[save.party.size() - 1]
	assert_eq(last.item, MAIL_ITEM)
	assert_not_null(last.mail)
	assert_eq(last.mail.item, MAIL_ITEM)
	assert_eq(last.mail.author, last.original_trainer)
	assert_eq(last.mail.author_id, last.ot_id)
	## The break sits behind a fifteen-letter line here, where the naming screen
	## would put it at `LINE_LENGTH`: the split is found, not assumed.
	assert_eq(last.mail.line(0), MAIL_LINE_1)
	assert_eq(last.mail.line(1), MAIL_LINE_2)


## The five `POKEMAIL_*` answers Route 31 branches on, in the order
## `CheckPokeMail` tests them.
func test_checkpokemail_answers_refused_when_the_list_is_backed_out_of() -> void:
	await _run_mail_script(
		Gen2WorldScript.CHECKPOKEMAIL, _mail_message(MAIL_LINE_1, MAIL_LINE_2)
	)
	assert_not_null(_selection_list())
	_selection_list().handle_button(PokeButton.B)
	assert_eq(
		_world_screen._world._active_script._script_value,
		Gen2WorldPartyHost.POKEMAIL_REFUSED
	)


func test_checkpokemail_answers_no_mail_for_an_empty_hand() -> void:
	await _run_mail_script(
		Gen2WorldScript.CHECKPOKEMAIL, _mail_message(MAIL_LINE_1, MAIL_LINE_2)
	)
	_selection_list().handle_button(PokeButton.A)
	assert_eq(
		_world_screen._world._active_script._script_value,
		Gen2WorldPartyHost.POKEMAIL_NO_MAIL
	)


func test_checkpokemail_answers_wrong_mail_for_another_message() -> void:
	await _run_mail_script(
		Gen2WorldScript.CHECKPOKEMAIL, _mail_message(MAIL_LINE_1, MAIL_LINE_2)
	)
	_carry_mail(
		_world_screen.active_save().party[0], _mail_message(MAIL_LINE_1, "somewhere else")
	)
	_selection_list().handle_button(PokeButton.A)
	assert_eq(
		_world_screen._world._active_script._script_value,
		Gen2WorldPartyHost.POKEMAIL_WRONG_MAIL
	)
	assert_eq(_world_screen.active_save().party.size(), 2, "nothing is handed over")


## `CheckCurPartyMonFainted` is read past the compare, so the right mail on the
## only member who can still walk is still refused.
func test_checkpokemail_answers_last_mon_when_no_other_member_can_fight() -> void:
	var message: PackedByteArray = _mail_message(MAIL_LINE_1, MAIL_LINE_2)
	await _run_mail_script(Gen2WorldScript.CHECKPOKEMAIL, message)
	_leave_one_member()
	_carry_mail(_world_screen.active_save().party[0], message)
	_selection_list().handle_button(PokeButton.A)
	assert_eq(
		_world_screen._world._active_script._script_value,
		Gen2WorldPartyHost.POKEMAIL_LAST_MON
	)
	assert_eq(_world_screen.active_save().party.size(), 1)


func test_checkpokemail_hands_the_member_over_on_the_right_message() -> void:
	var message: PackedByteArray = _mail_message(MAIL_LINE_1, MAIL_LINE_2)
	await _run_mail_script(Gen2WorldScript.CHECKPOKEMAIL, message)
	var save: Gen2SaveData = _world_screen.active_save()
	var mate: Gen2SaveMon = save.party[1]
	_carry_mail(save.party[0], message)
	_selection_list().handle_button(PokeButton.A)
	assert_eq(
		_world_screen._world._active_script._script_value,
		Gen2WorldPartyHost.POKEMAIL_CORRECT
	)
	assert_eq(save.party.size(), 1, "RemoveMonFromPartyOrBox with REMOVE_PARTY")
	assert_eq(save.party[0], mate, "the slot behind the errand moves up")
