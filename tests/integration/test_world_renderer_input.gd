extends GutTest

## Scene integration for the renderer input hook. The fixture is synthetic, but
## the world screen and mod host are the production paths.
##
## The contract under test is an order, not a keymap: the screen claims what it
## needs and offers the rest, so a renderer can own a camera and still never be
## in a position to move the player.

const Fixture := preload("res://tests/integration/world_trainer_fixture.gd")

const RENDERER_SOURCE: String = """extends Node2D

var seen: Array = []

func set_world(_world, _animation = null) -> void:
	pass

func set_time_of_day(_time_of_day: int) -> void:
	pass

func refresh() -> void:
	pass

func refresh_animation() -> void:
	pass

func handle_world_input(event) -> bool:
	seen.append(event.keycode)
	return event.keycode == KEY_Q
"""

var _data: GameData = null
var _world_screen: Gen2WorldScreen = null


func before_each() -> void:
	_forget_view()
	_data = Fixture.build()
	_data = GameData.open_directory(Fixture.directory())
	Gen2ModHost.reset()


func after_each() -> void:
	if is_instance_valid(_world_screen):
		_world_screen.free()
		_world_screen = null
	Gen2ModHost.reset()
	_forget_view()
	RomCache.clear(Fixture.directory())


## Choosing a view writes the installation's own file, so a test that chooses one
## puts it back rather than leaving the player on a renderer a test registered.
func _forget_view() -> void:
	DirAccess.remove_absolute(Gen2ModState.PATH)
	Gen2ModState.reload()


func _open_world_with_renderer() -> Node:
	var script := GDScript.new()
	script.source_code = RENDERER_SOURCE
	script.reload()
	assert_true(Gen2ModHost.instance().register_world_renderer(&"camera", script)["ok"])
	assert_true(Gen2ModHost.instance().select_view(&"camera")["ok"])
	var packed: PackedScene = load("res://game/world/world_screen.tscn")
	_world_screen = packed.instantiate() as Gen2WorldScreen
	_world_screen.map_group = Fixture.MAP_GROUP
	_world_screen.map_number = Fixture.MAP_NUMBER
	_world_screen.start_cell = Vector2i(7, 6)
	var world := Gen2WorldAPI.open(
		_data, Fixture.MAP_GROUP, Fixture.MAP_NUMBER, Vector2i(7, 6)
	)
	var save := Gen2SaveStore.create_development_save(_data, 0)
	save.world = world.snapshot()
	_world_screen.set_data(_data)
	_world_screen.set_save(save)
	add_child(_world_screen)
	await get_tree().process_frame
	return _world_screen._renderer


## Both codes, the way a real key event arrives: bindings match on the physical
## one so a layout that does not spell WASD keeps the d-pad in those positions.
func _press(keycode: Key) -> InputEventKey:
	var key := InputEventKey.new()
	key.keycode = keycode
	key.physical_keycode = keycode
	key.pressed = true
	return key


func _pad(button: JoyButton) -> InputEventJoypadButton:
	var event := InputEventJoypadButton.new()
	event.button_index = button
	event.pressed = true
	return event


## A game menu is driven by the eight buttons and nothing else, so what a pad has
## to prove is that its own events make them: a face button, the d-pad, and the
## [InputEventAction] a held direction repeats as ([Gen2InputRuntime]).
func test_a_controller_opens_and_walks_the_start_menu() -> void:
	await _open_world_with_renderer()
	_world_screen._unhandled_input(_pad(JOY_BUTTON_START))
	await get_tree().process_frame
	var host: Gen2StartMenuScreen = _world_screen._start_menu_host
	assert_not_null(host, "START on a pad opens the pause menu")
	var first: int = host.cursor()

	_world_screen._unhandled_input(_pad(JOY_BUTTON_DPAD_DOWN))
	var second: int = host.cursor()
	assert_ne(second, first, "the d-pad moves the cursor")

	var repeat := InputEventAction.new()
	repeat.action = Gen2Button.action(Gen2Button.DOWN)
	repeat.pressed = true
	_world_screen._unhandled_input(repeat)
	assert_ne(host.cursor(), second, "a held direction keeps moving it")


func test_a_renderer_receives_the_input_the_screen_does_not_use() -> void:
	var renderer: Node = await _open_world_with_renderer()
	assert_true(renderer.has_method(Gen2ModHost.RENDERER_INPUT_METHOD))
	_world_screen._unhandled_input(_press(KEY_Q))
	assert_eq(renderer.get("seen"), [KEY_Q])


func test_a_renderer_never_receives_a_movement_or_interaction_button() -> void:
	var renderer: Node = await _open_world_with_renderer()
	var before: Vector2i = _world_screen._world.player_cell
	## The first press is `.CheckTurning`'s turn on the spot, since the player
	## spawns facing down; the walk is the press after it.
	_world_screen._unhandled_input(_press(KEY_RIGHT))
	while _world_screen._world.player_step_in_progress():
		_world_screen._world.advance_player_step_pass()
	_world_screen._unhandled_input(_press(KEY_RIGHT))
	_world_screen._unhandled_input(_press(KEY_SPACE))
	# The screen claimed both: the player moved, and neither key was offered on.
	assert_ne(_world_screen._world.player_cell, before)
	assert_eq(renderer.get("seen"), [])


func test_an_open_overlay_keeps_leftover_input_from_the_renderer() -> void:
	var renderer: Node = await _open_world_with_renderer()
	_world_screen._open_start_menu()
	await get_tree().process_frame
	assert_not_null(_world_screen._start_menu_host)
	_world_screen._unhandled_input(_press(KEY_Q))
	assert_eq(renderer.get("seen"), [])


func test_a_renderer_without_the_hook_leaves_the_screen_unchanged() -> void:
	var packed: PackedScene = load("res://game/world/world_screen.tscn")
	_world_screen = packed.instantiate() as Gen2WorldScreen
	_world_screen.map_group = Fixture.MAP_GROUP
	_world_screen.map_number = Fixture.MAP_NUMBER
	_world_screen.start_cell = Vector2i(7, 6)
	_world_screen.set_data(_data)
	add_child(_world_screen)
	await get_tree().process_frame
	# The built-in renderer takes no input, so an unused key stays unused.
	assert_false(_world_screen._renderer.has_method(Gen2ModHost.RENDERER_INPUT_METHOD))
	_world_screen._unhandled_input(_press(KEY_Q))
